$PROBLEM    
$INPUT      ID TIME AMT EVID DV
$DATA      data.csv IGNORE=@
$SUBROUTINE ADVAN2 TRANS2
$ABBR DECLARE T(NO)
$ABBR DECLARE DOWHILE J
$ABBR DECLARE INTEGER I

$PK
    

CL = THETA(1)*EXP(ETA(1))
V = THETA(2)*EXP(ETA(2))
KA = THETA(3)*EXP(ETA(3))
SC = V

$ERROR

IF (ICALL.EQ.4) THEN
IF(NEWIND.NE.2)I=0
IF(MDV.EQ.0)THEN
   I=I+1
   T(I)=TIME
   J=1
   DO WHILE (J <= I)
    CORRL2(J,1)=EXP(-0.6931/THETA(5)*(TIME-T(J)))
    J=J+1
   ENDDO
ENDIF
CALL SIMEPS(EPS)
ENDIF

IPRED = F
W = THETA(4)
IF(TIME.LT.4) W = 2*W
Y = IPRED + W*EPS(1)

$THETA  
(0, 2)
(0, 20)
(0, 0.2)
(0, 0.1)
(0, 4)
$OMEGA  
0.1 
0.1 
0.1 
$SIGMA  1.0  FIX
$SIMULATION (1234) NSUB = 1

$TABLE      ID TIME AMT EVID DV NOPRINT NOAPPEND ONEHEADER FILE=simdata.dat
